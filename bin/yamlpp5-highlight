#!/usr/bin/perl
use strict;
use warnings;

use Data::Dumper;
use YAML::PP;
use YAML::PP::Highlight;
use Encode;

my ($file) = @ARGV;
my $yaml;

unless ($file) {
    $yaml = do { local $/; <STDIN> };
    $yaml = decode_utf8($yaml);
}

my $ypp = YAML::PP->new;
my @docs = eval {
    $file ? $ypp->load_file($file) : $ypp->load_string($yaml);
};
my $error = $@;
my $tokens = $ypp->loader->parser->tokens;
my $next = $ypp->loader->parser->lexer->next_tokens;
if ($error) {
    push @$tokens, map { +{ %$_, name => 'ERROR' } } @$next;
    my $remaining = $ypp->loader->parser->lexer->reader->read;
    $remaining = '' unless defined $remaining;
    push @$tokens, { name => "ERROR", value => $remaining };
}
else {
    push @$tokens, @$next;
}
my $highlighted = YAML::PP::Highlight->ansicolored($tokens);
print encode_utf8 $highlighted;
if ($error) {
    die $error;
}
