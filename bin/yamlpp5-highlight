#!/usr/bin/perl
use strict;
use warnings;
use 5.010;

use Data::Dumper;
use YAML::PP::Loader;
use YAML::PP::Highlight;
use Encode;

my ($file) = @ARGV;
my $yaml;

if ($file) {
    $yaml = do { open my $fh, '<', $file or die $!; local $/; <$fh> };
}
else {
    $yaml = do { local $/; <STDIN> };
}
$yaml = decode_utf8($yaml);

my $ypp = YAML::PP::Loader->new;
my @docs = eval {
    $ypp->load($yaml);
};
my $error = $@;
my $tokens = $ypp->parser->tokens;
my $next = $ypp->parser->lexer->next_tokens;
if ($error) {
    push @$tokens, map { +{ %$_, name => 'ERROR' } } @$next;
    my $remaining = $ypp->parser->lexer->next_line;
    $$remaining .= $ypp->parser->lexer->reader->read;
    push @$tokens, { name => "ERROR", value => $$remaining };
}
else {
    push @$tokens, @$next;
}
my $highlighted = YAML::PP::Highlight->ansicolored($tokens);
print encode_utf8 $highlighted;
if ($error) {
    die $error;
}
